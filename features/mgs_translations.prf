
!isEmpty(TRANSLATIONS): contains(RESOURCES, translations.qrc) {

  # make sure the dir for .qm files exist
  mkpath($$_PRO_FILE_PWD_/translations)

  # its virtually impossible in a practical sence that
  # features would change TRANSLATIONS variable
  # so I do not implement any precautions agains this.
  _old_translations = $$cat($$OUT_PWD/translations.txt)
  _current_translations = $$TRANSLATIONS
  _current_translations -= $$_old_translations
  _old_translations -= $$TRANSLATIONS
  _transaltions_diff = $$_old_translations $$_current_translations
  !isEmpty(_transaltions_diff)|!exists($$OUT_PWD/translations.txt)|!exists($$_PRO_FILE_PWD_/translations.qrc) {
    message(regenerating translations.qrc)

    # generate translations.qrc file
    _translations_qrc += <RCC><qresource prefix=\"/\">$$escape_expand(\n)
    for(_translation_name, TRANSLATIONS) {
      _translation_name_qm = $$section(_translation_name,".", 0, 0).qm
      _translations_qrc += <file>translations/$$_translation_name_qm</file>$$escape_expand(\n)
      # first time .qm generation
      # qrc will have depndencies on its resources only if the files exist on QMAKE_EXTRA_COMPILERS phase.
      # .ts files have to exist - run lupdate once before the first project build.
      system($$shell_path($$[QT_INSTALL_BINS]/lrelease) $$shell_path($$_PRO_FILE_PWD_/$$_translation_name) -qm $$shell_path($$_PRO_FILE_PWD_/translations/$$_translation_name_qm))
    }
    _translations_qrc += </qresource></RCC>$$escape_expand(\n)
    write_file($$_PRO_FILE_PWD_/translations.qrc, _translations_qrc)

    write_file($$OUT_PWD/translations.txt, TRANSLATIONS);
  }

  # make sure lrelease is launched and .qm files are refreshed when .ts files are changed
  QMAKE_EXTRA_COMPILERS += _translations_lrelease
  _translations_lrelease.input = TRANSLATIONS
  _translations_lrelease.output = $$_PRO_FILE_PWD_/translations/${QMAKE_FILE_BASE}.qm
  _translations_lrelease.commands = $$[QT_INSTALL_BINS]/lrelease ${QMAKE_FILE_IN} -qm ${QMAKE_FILE_OUT}
  _translations_lrelease.CONFIG += no_link

  # just for completenes
  # with Makefile deletes clean does not work
  QMAKE_CLEAN += $$shell_path($$OUT_PWD/translations.txt)
}